/*
 * Copyright (C) 2015-2016 KeepSafe Software
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    apply from: rootProject.file("gradle/versions.gradle")

    repositories {
        maven { url "https://plugins.gradle.org/m2/" } // Mirrors jcenter() and mavenCentral()
    }

    dependencies {
        classpath plugs.gradleVersionsPlugin // Check for plugin updates
        classpath plugs.kotlinGradlePlugin
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" } // Mirrors jcenter() and mavenCentral()
        google()
    }
}

apply plugin: "groovy"
apply plugin: "java-gradle-plugin"
apply plugin: "kotlin"
apply plugin: "com.github.ben-manes.versions"

sourceCompatibility = rootProject.ext.sourceCompatibilityVersion

group = GROUP
version = VERSION_NAME

jar {
    manifest {
        attributes(
                "Implementation-Title": POM_ARTIFACT_ID,
                "Implementation-Version": VERSION_NAME)
    }
}

dependencies {
    implementation localGroovy()
    implementation deps.kotlinStdlib
    implementation deps.gson

    compileOnly plugs.gradle

    testImplementation plugs.gradle
    testImplementation deps.junit
    testImplementation deps.spockCore, { exclude module: "groovy-all" } // Use localGroovy()
}

apply from: rootProject.file("gradle/gradle-mvn-push.gradle")
apply from: rootProject.file("gradle/compile.gradle")

// Compile Kotlin then Groovy
afterEvaluate {
    compileKotlin.dependsOn = compileKotlin.taskDependencies.values - "compileJava"
    compileGroovy.dependsOn compileKotlin
    compileGroovy.classpath += files(compileKotlin.destinationDir)
    classes.dependsOn compileGroovy
}
