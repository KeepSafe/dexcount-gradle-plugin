/*
 * Copyright (C) 2015-2016 KeepSafe Software
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "com.github.ben-manes.versions" version "0.29.0"
    id "groovy"
    id "com.microsoft.thrifty" version "2.1.1"
    id "java-gradle-plugin"
    id "com.vanniktech.maven.publish" version "0.17.0"
    id "com.gradle.plugin-publish" version "0.15.0"
}

group = GROUP
version = VERSION_NAME
description = POM_DESCRIPTION

sourceCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    integrationTest
}

configurations {
    integrationTestImplementation {
        extendsFrom testImplementation
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Title": POM_ARTIFACT_ID,
                "Implementation-Version": VERSION_NAME)
    }
}

repositories {
    mavenLocal {
        content {
            includeGroup "com.getkeepsafe.dexcount"
        }
    }
    gradlePluginPortal()
    google()
}

def agpVersion = "7.3.0-alpha06"
def autoValueVersion = "1.8.1"
def gsonVersion = "2.8.6"
def javassistVersion = "3.27.0-GA"
def thriftyVersion = "3.0.0"

dependencies {
    implementation 'commons-io:commons-io:2.10.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    compileOnly "com.google.code.gson:gson:${gsonVersion}"
    compileOnly "org.javassist:javassist:${javassistVersion}"
    compileOnly "com.microsoft.thrifty:thrifty-runtime-jvm:${thriftyVersion}"

    compileOnlyApi "com.google.auto.value:auto-value-annotations:${autoValueVersion}"
    annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"

    compileOnly "com.android.tools.build:gradle:$agpVersion"
    compileOnly "com.android.tools:r8:2.2.64"
    compileOnly "com.android.tools:repository:30.3.0-alpha06"

    testImplementation "com.android.tools.build:gradle:$agpVersion"
    testImplementation "com.google.code.gson:gson:${gsonVersion}"
    testImplementation "com.microsoft.thrifty:thrifty-runtime:${thriftyVersion}"
    testImplementation "org.codehaus.groovy:groovy-all:3.0.9"
    testImplementation "org.javassist:javassist:${javassistVersion}"
    testImplementation dependencies.create("org.spockframework:spock-core:2.0-groovy-3.0") {
        exclude module: "groovy-all"
    }
}

thrifty {
    java {}
}

def generateDependencyResource = tasks.register("generateDependencyResource") { t ->
    def generatedResourcesDir = project.layout.buildDirectory.dir(["generated", "sources", "dexcount", "src", "main", "resources"].join(File.separator))
    def outputFile = generatedResourcesDir.map { it -> it.file("dependencies.list") }

    t.inputs.property("gson-version", gsonVersion)
    t.inputs.property("javassist-version", javassistVersion)
    t.inputs.property("thrifty-version", thriftyVersion)

    t.outputs.dir(generatedResourcesDir)

    t.doFirst {
        File file = outputFile.get().getAsFile()
        file.getParentFile().mkdirs()
        file.delete()

        file << "com.google.code.gson:gson:${gsonVersion}\n"
        file << "org.javassist:javassist:${javassistVersion}\n"
        file << "com.microsoft.thrifty:thrifty-runtime-jvm:${thriftyVersion}\n"
    }
}

sourceSets {
    main {
        resources {
            srcDirs += generateDependencyResource
        }
    }
}

def installForTesting = tasks.register("installForIntegrationTests") { t ->
    t.dependsOn "publishToMavenLocal"
}

def integrationTest = tasks.register("integrationTest", Test) { t ->
    t.dependsOn installForTesting
    t.mustRunAfter tasks.named("test")

    t.outputs.cacheIf { false }
    t.outputs.upToDateWhen { false }

    t.group "verification"
    t.description "Runs integration tests."

    t.testClassesDirs = sourceSets.integrationTest.output.classesDirs
    t.classpath = sourceSets.integrationTest.runtimeClasspath

    // Workaround for https://github.com/gradle/gradle/issues/4506#issuecomment-570815277
    t.systemProperty("org.gradle.testkit.dir", file("${buildDir}/tmp/.test-kit"))

    t.jvmArgs(
        "-XX:+HeapDumpOnOutOfMemoryError", "-XX:GCTimeLimit=20", "-XX:GCHeapFreeLimit=10",
        "-XX:MaxMetaspaceSize=2g"
    )

    t.beforeTest { testCase ->
        logger.lifecycle("Running test: $testCase")
    }
}

tasks.named("check").configure { t ->
    t.dependsOn integrationTest
}

tasks.register("publishEverywhere") { t ->
    t.group "publishing"
    t.description "Publish to Maven Central and the Gradle Plugin Portal"

    t.dependsOn("uploadArchives", "publishPlugins")
}

// Compiler settings

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    // Show all warnings except boot classpath
    configure(options) {
        compilerArgs << "-Xlint:all"            // Turn on all warnings
        compilerArgs << "-Xlint:-options"       // Turn off "missing" bootclasspath warning
        compilerArgs << "-Xlint:-processing"    // Turn off "no processor claimed these annotatiosn" warning
        compilerArgs << "-Werror"               // Turn warnings into errors
        encoding = "utf-8"
        fork = true
    }
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    // Show all warnings except boot classpath
    configure(options) {
        compilerArgs << "-Xlint:all"            // Turn on all warnings
        compilerArgs << "-Xlint:-options"       // Turn off "missing" bootclasspath warning
        compilerArgs << "-Werror"               // Turn warnings into errors
        incremental = true
        encoding = "utf-8"
        fork = true
    }

    configure(groovyOptions) {
        encoding = "utf-8"
        fork = true
    }
}

// Don't fork too much on CI - it tends to run out of metaspace memory.
def isCi = providers.environmentVariable("CI").forUseAtConfigurationTime().isPresent()

tasks.withType(Test) {
    // Turn on logging for all tests, filter to show failures/skips only
    testLogging {
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
        events "failed", "skipped"
    }

    useJUnitPlatform()

    failFast = true
    maxParallelForks = isCi ? 1 : Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

tasks.withType(Javadoc) {
    title = "${project.name} ${project.version}"
    configure(options) {
        source = JavaVersion.VERSION_1_8
        header = project.name
        encoding "UTF-8"
        docEncoding "UTF-8"
        charSet "UTF-8"
        linkSource true
        author = true
        links("https://docs.oracle.com/javase/8/docs/api/",
            "https://docs.oracle.com/javaee/7/api/")
        exclude "**/*Test.java"
        if (JavaVersion.current().java8Compatible) addStringOption("Xdoclint:none", "-quiet")
    }
}

tasks.withType(Groovydoc) {
    docTitle = "${project.name} ${project.version}"
    header = project.name
    link("http://docs.oracle.com/javase/8/docs/api/",
        "http://docs.oracle.com/javaee/7/api/",
        "http://groovy.codehaus.org/gapi/")
    exclude "**/*Spec.java"
}

// Plugin Portal publishing

gradlePlugin {
    plugins {
        dexcount {
            id = "com.getkeepsafe.dexcount"
            implementationClass = "com.getkeepsafe.dexcount.DexMethodCountPlugin"
            displayName = POM_NAME
            description = POM_DESCRIPTION
        }
    }
}

pluginBundle {
    website = POM_URL
    vcsUrl = POM_SCM_URL
    description = POM_DESCRIPTION
    tags = ['android', 'dex', 'method count']

    plugins {
        dexcount {
            id = "com.getkeepsafe.dexcount"
            displayName = POM_NAME
            description = POM_DESCRIPTION
        }
    }

    mavenCoordinates {
        groupId = GROUP
        artifactId = POM_ARTIFACT_ID
        version = VERSION_NAME
    }
}

// Convenient entrypoint for the "upload snapshot" CI action.
// It's easier to do the version check here than in a Github action.
def isSnapshot = providers.gradleProperty("VERSION_NAME").forUseAtConfigurationTime().map { it.endsWith("-SNAPSHOT") }
tasks.register("uploadSnapshot") {
    if (isSnapshot.get()) {
        dependsOn tasks.named("publish")
    } else {
        doFirst {
            logger.lifecycle("Skipping upload of non-snapshot version '{}'", VERSION_NAME)
        }
    }
}

wrapper {
    gradleVersion = "7.4"
    distributionType = Wrapper.DistributionType.ALL
}
