/*
 * Copyright (C) 2015-2016 KeepSafe Software
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" } // Mirrors jcenter() and mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.0"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.14.0"
    }
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" } // Mirrors jcenter() and mavenCentral()
}

apply plugin: "groovy"
apply plugin: "kotlin"
apply plugin: "java"
apply plugin: "com.github.ben-manes.versions"

sourceCompatibility = JavaVersion.VERSION_1_7

group = GROUP
version = VERSION_NAME

jar {
    manifest {
        attributes(
                "Implementation-Title": POM_ARTIFACT_ID,
                "Implementation-Version": VERSION_NAME)
    }
}

sourceSets {
    main.java.srcDirs += "src/main/kotlin"
}

dependencies {
    compile gradleApi()
    compile "com.android.tools.build:gradle:2.3.0"
    compile "com.google.code.gson:gson:2.8.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:1.1.0" // Make sure to use jre7

    testCompile "junit:junit:4.12"
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") { exclude module: "groovy-all" }
}

apply from: file("gradle/gradle-mvn-push.gradle")

// Compile Kotlin then Groovy
afterEvaluate {
    compileKotlin.dependsOn = compileKotlin.taskDependencies.values - "compileJava"
    compileGroovy.dependsOn compileKotlin
    compileGroovy.classpath += files(compileKotlin.destinationDir)
    classes.dependsOn compileGroovy
}
